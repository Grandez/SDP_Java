package com.jgg.sdp.parser.lang;

import java_cup.runtime.*;

import com.jgg.sdp.core.ctes.*;
import com.jgg.sdp.core.config.*;

import com.jgg.sdp.parser.base.*;
import com.jgg.sdp.parser.base.stmt.*;
import com.jgg.sdp.parser.code.*;

parser code {:
      Configuration cfg  = Configuration.getInstance();
      ParserInfo    info = ParserInfo.getInstance();

      public void syntax_error(Symbol token) {
           
          Symbol s = (Symbol) token.value;
          int col = cfg.getInteger(CFG.MARGIN_LEFT,  0);
          col = col + s.right + 1;

          throw new ParseException(MSG.EXCEPTION_SYNTAX, 
                                   info.getModuleName(), 
                                   s.left + info.getOffset(), 
                                   col, 
                                   (String) s.value);
      }

      public void unrecovered_syntax_error(Symbol token) throws Exception {
          Symbol s = (Symbol) token.value;
          throw new ParseException(MSG.EXCEPTION_CUP, 
                                  info.getModuleName(), s.left + info.getOffset(), s.right + 1, (String) s.value); 
      }
:}

action code {:

   CICSCode    code     = new CICSCode();
   StmtCICS    stmt     = null;
   ParserInfo      info     = ParserInfo.getInstance();
   ComplexSymbolFactory symbolFactory = new ComplexSymbolFactory();

   public void initActionClass() {
   }

   public String getSymbolName(Symbol s)    { return (String) s.value; }
   public void debug(String txt)            { System.err.println(txt); }

:}

init with {:
  action_obj.initActionClass();
:}


/****************************************************************************/
/****************************************************************************/
/*               VERBOS                                                     */
/****************************************************************************/
/****************************************************************************/

terminal Symbol DECIMAL   ;
terminal Symbol ENTERO    ;
terminal Symbol ID        ; 
terminal Symbol IN        ;
terminal Symbol LENGTH    ;
terminal Symbol LITERAL   ;
terminal Symbol LPAR      ;
terminal Symbol OF        ;
terminal Symbol RPAR      ;

/****************************************************************************/
/****************************************************************************/
/*               NO TERMINALES                                              */
/****************************************************************************/
/****************************************************************************/

non terminal Symbol   cics            ;
non terminal StmtCICS cics_stmts      ;
non terminal StmtCICS cics_statement  ;
non terminal StmtCICS cics_stmt       ;

non terminal Option cics_options    ;
non terminal Option cics_parm_list  ;
non terminal Option cics_parm_attr  ;

non terminal Symbol   cics_parm       ;
non terminal Symbol   cics_values     ;
non terminal Symbol   cics_value      ;
non terminal Symbol   number          ;

non terminal Symbol   ident_base  ;

non terminal Symbol     padres_lst  ;
non terminal Symbol     padres      ;
non terminal Symbol     padre       ;
non terminal Symbol     padre_indicator ;  


/**********************************************************************/
/**********************************************************************/
/**********************************************************************/
/*                        INICIO PARSER                               */
/**********************************************************************/
/**********************************************************************/
/**********************************************************************/

cics ::= cics_stmts:s  {: RESULT = symbolFactory.newSymbol(stmt.getVerbName(), stmt.getVerbId(), stmt);  :}   
     |                                                            
     ;
   
cics_stmts ::= cics_statement:s             {: RESULT = code.processCICSStatement(s); :}
           ;
           
cics_statement ::= cics_stmt:s cics_options:o {: RESULT = s; :}
               ;
               
cics_stmt ::= ID:i    {: stmt = new StmtCICS(i); RESULT = stmt; :}
          ;
          
cics_options ::= cics_parm_list:c {: RESULT = c;    :}
             |                    {: RESULT = null; :}
             ;
             
cics_parm_list ::= cics_parm_attr:c                {: RESULT = stmt.addParm(c); :}
               |   cics_parm_list cics_parm_attr:c {: RESULT = stmt.addParm(c); :} 
               ;
                                 
cics_parm_attr ::= ID:i cics_parm:c      {: RESULT = new Option(i,c); :}
               |   LENGTH:i cics_parm:c  {: RESULT = new Option(i,c); :}
               ;
               
cics_parm ::= LPAR cics_values:c RPAR   {: RESULT = c;    :}
          |                             {: RESULT = null; :}
          ;             

cics_values ::= cics_value:c             {: RESULT = c; :} 
            |   cics_values:c cics_value {: RESULT = c; /* Nos interesa el primero */ :}
            ;
            
cics_value ::= ident_base:i           {: RESULT = i; :}
           |   number:n               {: RESULT = n; :}
           |   LITERAL:l              {: RESULT = l; :}
           |   LENGTH OF ident_base:i {: RESULT = i; :}
           |   LPAR cics_values:i RPAR {: RESULT = i; :}
           ;


number ::= ENTERO:e    {: RESULT = e; :}
       |   DECIMAL:d   {: RESULT = d; :} 
       ;                                                      
       
       
ident_base ::= ID:i padres_lst:s {: RESULT = i; :} 
           ;

padres_lst ::= padres:p  {: RESULT = p;    :}
           |             {: RESULT = null; :}
           ;

padres ::= padre:p           {: RESULT = p; :}
       |   padres:p padre:i  {: RESULT = i; :}
       ;
                    
padre ::= padre_indicator ID:s {: RESULT = s;    :}
      ;

padre_indicator ::= OF
                |   IN
                ;
       