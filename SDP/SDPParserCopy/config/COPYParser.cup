package com.jgg.sdp.parser.copy.lang;

import java_cup.runtime.*;

import com.jgg.sdp.module.base.*;
import com.jgg.sdp.module.items.*;
import com.jgg.sdp.module.factorias.*;
import com.jgg.sdp.core.ctes.*;
import com.jgg.sdp.core.config.*;
import com.jgg.sdp.core.exceptions.*;

import com.jgg.sdp.parser.base.*;
import com.jgg.sdp.parser.base.stmt.*;
import com.jgg.sdp.parser.base.symbol.*;

parser code {:
      Configuration cfg  = Configuration.getInstance();
      ParserInfo    info = ParserInfo.getInstance();
             
      public void syntax_error(Symbol token) {
           
          Symbol s = (Symbol) token.value;
          int col = cfg.getInteger(CFG.MARGIN_LEFT,  0);
          col = col + s.right + 1;

          throw new ParseException(MSG.EXCEPTION_SYNTAX, 
                                   info.getMemberName(), 
                                   s.left + info.getOffset(), 
                                   col, 
                                   (String) s.value);
      }

      public void unrecovered_syntax_error(Symbol token) throws Exception {
          Symbol s = (Symbol) token.value;
          throw new ParseException(MSG.EXCEPTION_CUP, 
                                   info.getMemberName(), 
                                   s.left + info.getOffset(),
                                   s.right + 1, 
                                   (String) s.value); 
      }
:}

action code {:
   ParserInfo    info    = ParserInfo.getInstance();      
   
   ComplexSymbolFactory symbolFactory = new ComplexSymbolFactory();
         
   StmtCopy stmt = null;  // Instruccion en proceso
   
   public void initActionClass() {
      
   }

   public void debug(String txt) { System.err.println(txt); }
   public void print(String txt) { 
      System.out.println(txt); 
   }

:}

init with {:
  action_obj.initActionClass();
:}


/****************************************************************************/
/****************************************************************************/
/*               TERMINALES                                                 */
/****************************************************************************/
/****************************************************************************/

terminal Symbol SUPPRESS  ;
terminal Symbol REPLACING ;
terminal Symbol ID        ;
terminal Symbol IN        ;
terminal Symbol OF        ;

/****************************************************************************/
/****************************************************************************/
/*               NO TERMINALES                                              */
/****************************************************************************/
/****************************************************************************/

non terminal Symbol     copy        ; 

non terminal StmtCopy   cpy_stmt    ;
non terminal Symbol     cpy_member  ;
non terminal            cpy_options ;
         
non terminal            cpy_supress_opt     ;
non terminal            cpy_replacing_opt   ;
non terminal            cpy_replacing       ;
non terminal Symbol     cpy_member_id       ;
non terminal Option     cpy_lib_opt         ;
non terminal SymbolList cpy_replacing_lst   ;
non terminal Symbol     cpy_replacing_token ;

/**********************************************************************/
/**********************************************************************/
/**********************************************************************/
/*                        INICIO PARSER                               */
/**********************************************************************/
/**********************************************************************/
/**********************************************************************/

copy ::=  cpy_stmt:c  {: RESULT = symbolFactory.newSymbol(stmt.getVerbName(), stmt.getVerbId(), stmt);  :}
     ; 

/*********************************************************************/
/* COPYS                                                             */
/*********************************************************************/

cpy_stmt ::= cpy_member:m cpy_options 
         ;
         
cpy_member ::= cpy_member_id:c cpy_lib_opt:p   {: stmt = new StmtCopy(c); 
                                                  stmt.addOption(p); 
                                                :}
           ;

cpy_lib_opt ::= IN:c cpy_member_id:p {: RESULT = new Option(c, p);  :}
            |   OF:c cpy_member_id:p {: RESULT = new Option(c, p);  :}
            |                        {: RESULT = null; :}
            ;
         
cpy_options ::= cpy_supress_opt cpy_replacing_opt
            ;
                          

cpy_member_id ::= ID:s {: RESULT = s; :}
              ;

                 
cpy_supress_opt ::= SUPPRESS:s  {: stmt.addOption(new Option(s)); :}
                |
                ;
             
cpy_replacing_opt ::= cpy_replacing
                  |
                  ;

cpy_replacing ::= REPLACING:s  cpy_replacing_lst:l {: Option opt = new Option(s);
                                                      opt.add(l);
                                                      stmt.addOption(opt); 
                                                    :}
              ;
              
cpy_replacing_lst ::= cpy_replacing_token:c                     {: RESULT = new SymbolList(c); :}
                  |   cpy_replacing_lst:l cpy_replacing_token:c {: RESULT = l.add(c);          :}
                  ;
                       
cpy_replacing_token ::= ID:s         {: RESULT = s; :}
                    ;
                                                                                                    
